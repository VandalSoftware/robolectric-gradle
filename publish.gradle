apply plugin: 'maven'
apply plugin: 'signing'

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn:classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

signing {
    if (project.ext.release) {
        sign configurations.archives
    }
}

uploadArchives {
    onlyIf {
        (project.ext.repositoryUrl) &&
                project.ext.repositoryPassword && project.ext.repositoryUsername
    }
    repositories {
        mavenDeployer {
            configuration = configurations.archives
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
            }
            repository(url: project.ext.repositoryUrl) {
                authentication(userName: project.ext.repositoryUsername,
                        password: project.ext.repositoryPassword)
            }
            pom.artifactId = 'robolectric'
            pom.project {
                name 'Robolectric Gradle Plugin'
                packaging 'jar'
                description 'Robolectric Gradle plugin'
                url 'https://github.com/VandalSoftware/robolectric-gradle'

                scm {
                    url 'scm:git@github.com:VandalSoftware/robolectric-gradle.git'
                    connection 'scm:git@github.com:VandalSoftware/robolectric-gradle.git'
                    developerConnection 'scm:git@github.com:VandalSoftware/robolectric-gradle.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'jle'
                        name 'Jonathan Le'
                        email 'jle@vandal.io'
                    }
                }
            }
        }
    }
}
